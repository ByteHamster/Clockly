plugins {
    id("com.android.application")
    id("com.github.triplet.play") version "2.3.0" apply false
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.bytehamster.clockly"
        minSdkVersion 19
        targetSdkVersion 29

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            if (project.hasProperty("SIGNING1")) {
                storeFile file(SIGNING1_STORE_FILE)
                storePassword SIGNING1_STORE_PASSWORD
                keyAlias SIGNING1_KEY_ALIAS
                keyPassword SIGNING1_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

if (project.hasProperty("TRIPLET_API_KEY")) {
    apply plugin: 'com.github.triplet.play'
    play {
        track = 'alpha'
        serviceAccountCredentials = file(TRIPLET_API_KEY)
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.percentlayout:percentlayout:1.0.0"
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation "com.google.android.material:material:1.2.0"
    testImplementation 'junit:junit:4.12'
}
